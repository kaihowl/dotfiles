# shellcheck disable=all

# Loaned and stripped down from zsh4humans
# Changed and adopted based on https://stackoverflow.com/a/48509425
# The original approach did not work with ssh auth forwarding; the SSH_AGENT_PID
# is not guaranteed to be set in that case.

function start-ssh-and-add-identity() {
  [[ -v commands[ssh-agent] ]] || return 0
  [[ -v commands[ssh-add] ]] || return 0

  function -ssh-agent-running() {
    ssh-add -l &> /dev/null
    [[ $? -ne 2 ]]
  }

  {
    -ssh-agent-running && return
    unset SSH_AGENT_PID SSH_AUTH_SOCK

    local env_file=~/.ssh-agent-$EUID
    [[ -r $env_file ]] && builtin source $env_file >/dev/null && -ssh-agent-running && return

    extra_opts=()
    if [[ "$(uname)" == "Darwin" ]]; then
      local extra_opts=("--apple-use-keychain")
    fi

    local tmp=$env_file.tmp.$$
    command ssh-agent -s >$tmp       || return
    builtin source $tmp >/dev/null   || return
    -ssh-agent-running               || return
    mv -f -- $tmp $env_file          || return
    ssh-add ${extra_opts[@]}         || return
  } always {
    builtin unfunction -- -ssh-agent-running
  }
}

