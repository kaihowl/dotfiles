PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2>$HOME/tmp/startlog.$$
    setopt xtrace prompt_subst
fi

# Do not use dirname and `cd` as this costs about 10 ms of start up time,
# instead use these unreadable but fast builtins.
# Get the canonical (A) parent directory (:h:h) of this source file (%)
export DOTS=${${(%):-%x}:A:h:h}

DISABLE_UPDATE_PROMPT="true"

# Cache the brew prefix for other tools to pick up
if [[ -z $BREW_PREFIX ]] && which brew &> /dev/null; then
  export BREW_PREFIX=$(brew --prefix)
fi

# Periodically (typically once a day), update from github
source $DOTS/script/check_for_upgrade.sh

# all of our zsh files
typeset -U config_files
config_files=($DOTS/**/*.zsh)
# Remove test scripts
config_files=(${config_files:#*/nvim/tests/test-*/*})

# load every completion before compinit to set up the fpath
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# load oh-my-zsh (which does compinit)
# source $DOTS/zsh/oh-my.zsh.preamble

# your project folder that we can `c [tab]` to
export PROJECTS=~/Documents/repos/

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

unset config_files

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# We don't want this and oh-my-zsh has no way to turn it off
# Hence, we brute-force this back
unsetopt autocd

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi
