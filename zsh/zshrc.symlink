# Do not use dirname and `cd` as this costs about 10 ms of start up time,
# instead use these unreadable but fast builtins.
# Get the canonical (A) parent directory (:h:h) of this source file (%)
export DOTS=${${(%):-%x}:A:h:h}
fpath=($DOTS/zsh $fpath)

autoload -U start-ssh-and-add-identity
start-ssh-and-add-identity

# Periodically (typically once a day), update from github
source "$DOTS/script/check_for_upgrade.sh"

PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2> "$HOME/tmp/startlog.$$"
    setopt xtrace prompt_subst
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Cache the brew prefix for other tools to pick up
if [[ -z $BREW_PREFIX ]] && which brew &> /dev/null; then
  export BREW_PREFIX=$(brew --prefix)
fi

# Emacs keybindings
bindkey -e

# all of our zsh files
typeset -U config_files
config_files=($DOTS/**/*.zsh)
# Remove test scripts
config_files=(${config_files:#*/nvim/tests/test-*/*})

# load every completion before compinit to set up the fpath
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

source ~/.powerlevel10k/powerlevel10k.zsh-theme

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# TODO(kaihowl) double check effect and use
ZSH_AUTOSUGGEST_MANUAL_REBIND=1
source ~/.zsh-autosuggestions/zsh-autosuggestions.zsh

source ~/.zsh-z/zsh-z.plugin.zsh

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Must come last, does the compinit for us
if [[ "$ZSH_DISABLE_COMPFIX" == true ]]; then
  # zstyle '*:compinit' arguments -u
  autoload -U compinit
  compinit -u
else
  source ~/.zsh-autocomplete/zsh-autocomplete.plugin.zsh
fi

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi
