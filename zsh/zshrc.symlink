# Do not use dirname and `cd` as this costs about 10 ms of start up time,
# instead use these unreadable but fast builtins.
# Get the canonical (A) parent directory (:h:h) of this source file (%)
export DOTS=${${(%):-%x}:A:h:h}

# Ensure running in tmux
if [ ! -v TMUX ]; then
  if tmux new-session -s "$(hostname -f)_dev.$$"; then
    # This returns on exit from the spawned session only
      case $(ps -o comm= -p "$PPID") in
          sshd|*/sshd)
            if [[ -n $GCM_SOCKET ]]; then
              rm -f "$GCM_SOCKET"
            fi
            ;;
      esac
    exit
  fi
fi

fpath=($DOTS/zsh $fpath)

if [ -e '/nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh' ]; then
  . '/nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh'
fi

autoload -U start-ssh-and-add-identity
start-ssh-and-add-identity

# Periodically (typically once a day), update from github
source "$DOTS/script/check_for_upgrade.sh"

PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2> "$HOME/tmp/startlog.$$"
    setopt xtrace prompt_subst
fi

autoload -U fix-ssh

# Save command history
# Borrowed from Apple global conf on /etc/zshrc
HISTFILE="${ZDOTDIR:-$HOME}/.zsh_history"
HISTSIZE=2000
SAVEHIST=1000

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Emacs keybindings
bindkey -e

# all of our zsh files
typeset -U config_files
config_files=($DOTS/**/*.zsh)
# Remove test scripts
config_files=(${config_files:#*/nvim/tests/test-*/*})

# load every completion before compinit to set up the fpath
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done


# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Must come last, does the compinit for us
if [[ "$ZSH_DISABLE_COMPFIX" == true ]]; then
  # zstyle '*:compinit' arguments -u
  autoload -U compinit
  compinit -u
else
  source ~/.nix-profile/share/zsh-autocomplete/zsh-autocomplete.plugin.zsh
fi

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi
